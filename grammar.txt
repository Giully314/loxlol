program    -> declaration* EOF ;

declaration -> variable_decl | statement ;

variable_decl -> "var" IDENTIFIER ( "=" expression )? ";" ;

statement  -> expr_stmt | print_stmt | block ;
block      -> "{" declaration* "}" ;
expr_stmt  -> expression ";" ;
print_stmt -> "print" expression ";" ;

expression -> assignment ;
assignment -> IDENTIFIER "=" assignment | equality
equality   -> comparison ( ("!=" | "==") comparison )* ;
comparison -> term ( (">" | ">=" | "<" | "<=") term )* ;
term       -> factor ( ("-" | "+") factor )* ;
factor     -> unary ( ("*" | "/") unary )* ;
unary      -> ("!" | "-") unary | primary ;
primary    -> NUMBER | STRING | "true" | "false" | "nil" | "(" expression ")" | IDENTIFIER ;